// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        /* app config */
        app_compile_sdk_version = 31
        app_buildtools_version = "31.0.0"
        app_min_sdk_version = 21
        app_target_sdk_version = 31

        /* tools, plugins, etc. */
        kotlin_version = '1.6.10'
        android_gradle_plugin_version = '7.0.4'
        gms_plugin_version = '4.3.10'
        firebase_crashlytics_plugin_version = '2.8.1'
        firebase_appdistribution_version = '2.2.0'
        detekt_plugin_version = '1.19.0'

        /* may be helpful https://developer.android.com/jetpack/androidx/versions/ */
        /* dependencies */
        coroutines_version = '1.6.0'
        cicerone_version = '5.0.0'
        koin_version = '3.1.5'

        androidx_appcompat_version = '1.4.1'
        androidx_core_version = '1.7.0'
        androidx_constrainlayout_version = '2.1.3'
        androidx_lifecycle_version = '2.4.0'
        androidx_fragment_version = '1.4.0'
        androidx_swiperefreshlayout_version = '1.1.0'
        androidx_annotation_version = '1.3.0'
        androidx_biometric_version = '1.1.0'
        androidx_room_version = '2.4.1'
        material_version = '1.5.0'

        fastadapter_version = '5.6.0'
        viewbindingpropertydelegate_version = '1.5.6'
        corbind_version = '1.5.3'
        glide_version = '4.12.0'
        materialprogressbar_version = '1.6.1'
        inputmask_version = '6.0.0'

        google_play_core_version = '1.10.3'
        google_play_services_auth_version = '20.0.1'
        google_play_services_auth_phone_version = '18.0.1'
        firebase_bom_version = '26.0.0'

        retrofit_version = '2.9.0'
        okhttp_version = '4.9.2'
        moshi_version = '1.13.0'
        chucker_version = '3.5.2'
        timber_version = '5.0.1'

        junit_version = '4.13.2'
        androidx_espresso_version = '3.4.0'
        androidx_test_core_version = '1.4.0'
        androidx_test_extjunit_version = '1.1.3'
        kakao_version = '2.4.0'
        mockk_version = '1.12.2'
        robolectric_version = '4.7.3'
    }

    repositories {
        google()
        mavenCentral()
        jcenter() // remove later
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:$gms_plugin_version"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$firebase_crashlytics_plugin_version"
        classpath "com.google.firebase:firebase-appdistribution-gradle:$firebase_appdistribution_version"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("$detekt_plugin_version")
}

detekt {
    toolVersion = "$detekt_plugin_version"
    config = files("config/detekt/detekt.yml")
    source = files(
            "${rootProject.rootDir}/app/src/main/java",
            "${rootProject.rootDir}/data/src/main/java",
            "${rootProject.rootDir}/mobile/src/main/java",
            "${rootProject.rootDir}/core/datacore/src/main/java",
            "${rootProject.rootDir}/core/navigation/src/main/java",
            "${rootProject.rootDir}/core/uicore/src/main/java",
            "${rootProject.rootDir}/feature/auth/src/main/java",
            "${rootProject.rootDir}/feature/security/core/src/main/java",
            "${rootProject.rootDir}/feature/security/core-ui/src/main/java",
            "${rootProject.rootDir}/feature/security/example/src/main/java",
            "${rootProject.rootDir}/playground/src/main/java",
            "${rootProject.rootDir}/showcase/src/main/java"

    )
    baseline = file("${rootProject.projectDir}/config/baseline.xml")
    reports {
        html {
            enabled = true
            destination = file("config/detekt/destination.html")
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter() // remove later
    }

    // for working room on m1
    // https://issuetracker.google.com/issues/174695268?pli=1
    configurations.all {
        resolutionStrategy {
            force 'org.xerial:sqlite-jdbc:3.34.0'
        }
    }
}

task clean(type: Delete) {
    dependsOn gradle.includedBuilds*.task(":clean")
    delete rootProject.buildDir
}

apply plugin: 'android-reporting'